//Storage Account Misconfiguration Detection Using Azure Resource Graph Explorer

resources
| where type == "microsoft.storage/storageaccounts"
| extend
    networkAcls = properties.networkAcls,
    allowSharedKeyAccess = tostring(properties.allowSharedKeyAccess),
    supportsHttpsTrafficOnly = properties.supportsHttpsTrafficOnly,
    minimumTlsVersion = properties.minimumTlsVersion,
    allowBlobPublicAccess = tostring(properties.allowBlobPublicAccess),
    isHnsEnabled = properties.isHnsEnabled
| project
    name,
    resourceGroup,
    subscriptionId,
    kind,
    sku = sku.name,
    
    // Network Security
    publicNetworkAccess = iff(networkAcls.defaultAction == "Allow", "Public", "Restricted"),
    ipRestrictions = iff(isnotempty(networkAcls.ipRules), "Enabled", "None"),
    vnetRestrictions = iff(isnotempty(networkAcls.virtualNetworkRules), "Enabled", "None"),
    bypass = networkAcls.bypass,
    // Authentication
    sharedKeyAccess = case(
        isempty(allowSharedKeyAccess), "Unknown",
        allowSharedKeyAccess == "true", "Enabled",
        "Disabled"),
    // Encryption & TLS
    httpsOnly = iff(supportsHttpsTrafficOnly, "Enabled", "Disabled"),
    tlsVersion = minimumTlsVersion,
    // Data Protection
    blobPublicAccess = case(
        isempty(allowBlobPublicAccess), "Unknown",
        allowBlobPublicAccess == "true", "Enabled",
        "Disabled"),
    hierarchicalNamespace = iff(isHnsEnabled, "Enabled", "Disabled")
| where 
    publicNetworkAccess == "Public" or
    sharedKeyAccess == "Enabled" or
    httpsOnly == "Disabled" or
    tolower(tlsVersion) != "tls1_2" or
    blobPublicAccess == "Enabled"
| order by publicNetworkAccess desc, name asc

// Get Defender for App Services status per subscription
securityresources
| where type == "microsoft.security/pricings"
| where name == "AppServices"
| project subscriptionId, defenderStatus = tostring(properties.pricingTier)
| join kind=leftouter (
// Get App Services per subscription, with stack and plan info
    resources
    | where type =~ 'microsoft.web/sites'
    | extend 
        appServiceName = name,
        appServicePlanId = tostring(properties.serverFarmId),
        runtimeStack = tostring(properties.siteConfig.linuxFxVersion)
    | join kind=leftouter (
        resources
        | where type =~ "microsoft.web/serverfarms"
        | project 
            appServicePlanId = id,
            skuTier = tostring(sku.tier),
            skuName = tostring(sku.name)
    ) on appServicePlanId
    | project subscriptionId, appServiceName, skuTier, skuName, runtimeStack
) on subscriptionId
| join kind=inner (
    resourcecontainers
    | where type == "microsoft.resources/subscriptions"
    | project subscriptionId, subscriptionName = name
) on subscriptionId
| project
    subscriptionName,
    appServiceName = iff(isnull(appServiceName), "<No App Services>", appServiceName),
    skuTier = iff(isnull(skuTier), "<Unknown>", skuTier),
    skuName = iff(isnull(skuName), "<Unknown>", skuName),
    runtimeStack = iff(isnull(runtimeStack), "<Unknown>", runtimeStack),
    defenderStatus = case(
        defenderStatus == "Standard", "Enabled",
        defenderStatus == "Free", "Disabled",
        "NotConfigured"
    )
| order by defenderStatus asc, subscriptionName, appServiceName

securityresources
| where type == "microsoft.security/pricings"
| where name == "AppServices"
| project subscriptionId, defenderStatus = properties.pricingTier
| join kind=leftouter (
    resources
    | where type =~ 'microsoft.web/sites'
    | summarize appServiceCount = count() by subscriptionId
) on subscriptionId
| join kind=inner (
    resourcecontainers
    | where type == "microsoft.resources/subscriptions"
    | project subscriptionId, subscriptionName=name
) on subscriptionId
| project
    subscriptionName,
    defenderStatus = case(
        defenderStatus == "Standard", "Enabled",
        defenderStatus == "Free", "Disabled",
        "NotConfigured"
    ),
    appServiceCount = iff(isnull(appServiceCount), 0, appServiceCount)
| order by defenderStatus asc, appServiceCount desc
